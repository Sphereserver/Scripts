//****************************************************************************
// SPHERE by : Menasoft ©1997-2019
// www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Friday, Apr 12, 2019
//
VERSION=0.56d

[COMMENT DIALOG_HELP_SECTION]
dorigin x,y                                     // sets a dynamic origin point
nomove                                          // The gump cannot be moved around.
noclose                                         // The gump cannot be closed with a right click.
nodispose                                       // The gump cannot be closed by gump-closing macro.
page id                                         // For multi tab dialogs.
checkertrans x,y,width,height                   // add a transparent layer (only for clients >= 3)
resizepic x,y,backgroundgump,width,height       // can come first if multi page. puts up some background gump
gumppic x,y,gump,hue                            // put gumps in the dlg. (hue only for clients >= 3, otherwise ignored)
gumppictiled x,y,width,height,gump              // tile a gump
tilepic x,y,item                                // put item tiles in the dlg.
tilepichue x,y,item,hue                         // put colored item tiles in the dlg.
text x,y,color,stringindex                      // put some text here.
dtext x,y,color,text                            // put some text here.
croppedtext x,y,width,height,color,stringindex  // put some cropped text here.
dcroppedtext x,y,width,height,color,text        // put some cropped text here.
htmlgump x,y,width,height,stringindex,hasbackground,hasscrollbar                // add an html gump that shows a text
dhtmlgump x,y,width,height,hasbackgroud,hasscrollbar,text                       // add an html gump that show a text
xmfhtmlgump x,y,width,height,clilocid,hasbackground,hasscrollbar                // add an html gump that shows a cliloc text
xmfhtmlgumpcolor x,y,width,height,clilocid,hasbackground,hasscrollbar,color     // add an html gump that shows a colored cliloc text (color should be RGB value, not hue)
xmfhtmltok x,y,width,height,hasbackground,hasscrollbar,color,clilocid,@args@    // add an html gump that shows a cliloc text with arguments (Args seperator=@ -> @arg1@arg2@etc@)
button x,y,down_gump,up_gump,ispressable,pagedest,id                            // add a button
buttontileart x,y,down_gump,up_gump,ispressable,pagedest,id,tileid,tilehue,tilex,tiley      // add a button with a tilepic on top
textentry x,y,widthpix,widthchars,color,id,startstringindex                     // add an input text box
textentrylimited x,y,widthpix,widthchars,color,id,startstringindex,limit        // add an input text box with a "limit" maxchar (only for clients >= 5)
dtextentrylimited x,y,width,height,color,id,limit,text                          // textentry with a limit of characters
tooltip clilocid                                            // Popup a tooltip over a gump object (only for clients >= 4)
group id                                                    // Group a bunch of radios/checkboxes
radio x,y,gump1,gump2,starting_state,id                     // Add a radio button
checkbox x,y,gumpcheck,gumpuncheck,starting_state,checkid   // Add a checkbox

// NOTE: low button id's are reserved for page tabs.
// button id 0 is reserved for the CANCEL command
// TRYP arg3 INPDLG=arg1 arg2
//	arg1 = ITEM/CHAR property associated with this button
//	arg2 = formating for output/input (max length/max value)
//	arg3 = minimum privelege needed to change this (couns=2, seer=3, gm=4, developer=5, admin=6, never=255)

[DIALOG d_CHARPROP1]// prop sheet.
70, 70        // this is always the size.
page 0
resizepic 0 0 5100 600 440
text 30 30 0 0
button 10 405 247 248 1 0 900
//button 10 400 5200 5201 1 0 900
//button 90 400 5204 5205 1 0 901
//button 170 400 5202 5203 1 0 902
gumppic 325 15 2000
tilepic 400 140 <DISPIDDEC>
text 10 50 80 4
page 1
gumppic 4 2 5007
button 92 2 5006 5007 1 2 802
button 180 2 5006 5007 1 3 803
text 45 1 0 1
text 133 1 0 2
text 221 1 0 3
button 10 70 2443 2444 1 0 2
text 15 70 0 5
text 75 70 0 6
button 10 95 2443 2444 1 0 3
text 15 95 0 7
text 75 95 0 8
button 10 120 2443 2444 1 0 4
text 15 120 0 9
text 75 120 0 10
button 10 145 2443 2444 1 0 5
text 15 145 0 11
text 75 145 0 12
button 10 170 2443 2444 1 0 6
text 15 170 0 13
text 75 170 0 14
button 10 195 2443 2444 1 0 7
text 15 195 0 15
text 75 195 0 16
button 10 220 2443 2444 1 0 8
text 15 220 0 17
text 75 220 0 18
button 10 245 2443 2444 1 0 9
text 15 245 0 19
text 75 245 0 20
button 10 270 2443 2444 1 0 10
text 15 270 0 21
text 75 270 0 22
button 10 295 2443 2444 1 0 11
text 15 295 0 23
text 75 295 0 24
button 10 320 2443 2444 1 0 12
text 15 320 0 25
text 75 320 0 26
button 10 345 2443 2444 1 0 13
text 15 345 0 27
text 75 345 0 28
button 10 370 2443 2444 1 0 14
text 15 370 0 29
text 75 370 0 30
text 320 370 100 31
button 365 340 2443 2444 1 0 15
text 370 340 50 31
text 430 340 50 32
button 365 370 2443 2444 1 0 16
text 370 370 50 33
text 430 370 50 34
button 365 400 2443 2444 1 0 17
text 370 400 50 35
text 430 400 50 36
text 430 290 2100 37
text 150 370 2100 38
button 240 370 4011 4013 1 0 18

[DIALOG d_CHARPROP1 TEXT]
Character Properties 1
1
2
3
Change Value
Serial
<SERIAL>
Acct
<ACCOUNT>
Name
<NAME>
Title
<TITLE>
Color
<COLOR>
OSkin
<OSKIN>
ID
<ID>
Body
<BODY>
OBody
<OBODY>
Timer
<TIMER>
P
<P>
Home
<HOME>
Action
<ACTION>
NPC
<NPC>
Owner
<MEMORYFINDTYPE.02.LINK.NAME>
Speech
<SPEECH>
<DISPIDDEC>
Action Menu

[DIALOG d_CHARPROP1 BUTTON]

ON=802
	DIALOG d_CHARPROP2

ON=803
	DIALOG d_CHARPROP3

ON=2
	TRYP 255 INPDLG SERIAL 8

ON=3
	TRYP 6 INPDLG=ACCOUNT 30

ON=4
	TRYP 3 INPDLG=NAME 30

ON=5
	TRYP 4 INPDLG=TITLE 30

ON=6
	TRYP 3 INPDLG=COLOR 6

ON=7
	TRYP 3 INPDLG=OSKIN 6

ON=8
	TRYP 3 INPDLG=ID 30

ON=9
	TRYP 3 INPDLG=BODY 30

ON=10
	TRYP 3 INPDLG=OBODY 30

ON=11
	TRYP 4 INPDLG=TIMER 8

ON=12
	TRYP 3 INPDLG=P 16

ON=13
	TRYP 3 INPDLG=HOME 16

ON=14
	TRYP 4 INPDLG=ACTION 3

ON=15
	TRYP 4 INPDLG=NPC 4

ON=16
	SRC.SYSMESSAGE This belongs to <MEMORYFINDTYPE.02.NAME 8>

ON=17
	TRYP 4 INPDLG=SPEECH 20

ON=18
	IF ((<ACTION> >= SKILL_ANATOMY) && (<ACTION> <= SKILL_THROWING))	//1~57
	 SRC.MESSAGE @50 <DEFMSG.skillact_using> skill <SERV.SKILL.<EVAL <ACTION> & 0ff>.KEY>
	ELIF (<ACTION>==064)	//100
	 SRC.MESSAGE @50 <DEFMSG.skillact_following>
	ELIF (<ACTION>==065)	//101
	 SRC.MESSAGE @50 <DEFMSG.skillact_staying>
	ELIF (<ACTION>==066)	//102
	 SRC.MESSAGE @50 <DEFMSG.skillact_goingto>
	ELIF (<ACTION>==067)	//103
	 SRC.MESSAGE @50 <DEFMSG.skillact_wandering>
	ELIF (<ACTION>==068)	//104
	 SRC.MESSAGE @50 <DEFMSG.skillact_looking>
	ELIF (<ACTION>==069)	//105
	 SRC.MESSAGE @50 <DEFMSG.skillact_fleeing>
	ELIF (<ACTION>==06a)	//106
	 SRC.MESSAGE @50 <DEFMSG.skillact_talking>
	ELIF (<ACTION>==06b)	//107
	 SRC.MESSAGE @50 <DEFMSG.skillact_talkfollow>
	ELIF (<ACTION>==06c)	//108
	 SRC.MESSAGE @50 <DEFMSG.skillact_guarding>
	ELIF (<ACTION>==06d)	//109
	 SRC.MESSAGE @50 <DEFMSG.skillact_goinghome>
	ELIF (<ACTION>==06e)	//110
	 SRC.MESSAGE @50 <DEFMSG.skillact_breathing>
	ELIF (<ACTION>==06f)	//111
	 SRC.MESSAGE @50 <DEFMSG.skillact_ridden>
	ELIF (<ACTION>==070)	//112
	 SRC.MESSAGE @50 <DEFMSG.skillact_throwing>
	ELIF (<ACTION>==071)	//113
	 SRC.MESSAGE @50 <DEFMSG.skillact_training>
	ELIF (<ACTION>==072)	//114
	 SRC.MESSAGE @50 <DEFMSG.skillact_napping>
	ELIF (<ACTION>==073)	//115
	 SRC.MESSAGE @50 <DEFMSG.skillact_searchingfood>
	ELIF (<ACTION>==074)	//116
	 SRC.MESSAGE @50 <DEFMSG.skillact_runningto>
	ELSE
	 SRC.MESSAGE @50 <DEFMSG.skillact_idling>
	ENDIF

[DIALOG d_CHARPROP2]
70, 70
page 0
resizepic 0 0 5100 600 440
text 30 30 0 0
button 10 405 247 248 1 0 900
//button 10 400 5200 5201 1 0 900
//button 90 400 5204 5205 1 0 901
//button 170 400 5202 5203 1 0 902
gumppic 325 15 2000
tilepic 400 140 <DISPIDDEC>
text 10 50 80 4
page 1
button 4 2 5006 5007 1 1 801
gumppic 92 2 5007
button 180 2 5006 5007 1 3 803
text 45 1 0 1
text 133 1 0 2
text 221 1 0 3
button 10 70 2443 2444 1 0 1
text 15 70 1100 5
text 75 70 0 6
button 150 70 2443 2444 1 0 18
text 155 70 1100 38
text 215 70 0 39
button 10 95 2443 2444 1 0 2
text 15 95 3 7
text 75 95 0 8
button 150 95 2443 2444 1 0 19
text 155 95 3 40
text 215 95 0 41
button 10 120 2443 2444 1 0 3
text 15 120 450 9
text 75 120 0 10
button 150 120 2443 2444 1 0 20
text 155 120 450 42
text 215 120 0 43
button 10 145 2443 2444 1 0 4
text 15 145 1100 11
text 75 145 0 12
button 150 145 2443 2444 1 0 21
text 155 145 1100 44
text 215 145 0 45
button 10 170 2443 2444 1 0 5
text 15 170 3 13
text 75 170 0 14
button 150 170 2443 2444 1 0 22
text 155 170 3 46
text 215 170 0 47
button 10 195 2443 2444 1 0 6
text 15 195 450 15
text 75 195 0 16
button 150 195 2443 2444 1 0 23
text 155 195 450 48
text 215 195 0 49
button 10 220 2443 2444 1 0 7
text 15 220 0 17
text 75 220 0 18
button 10 245 2443 2444 1 0 8
text 15 245 0 19
text 75 245 0 20
button 10 270 2443 2444 1 0 9
text 15 270 0 21
text 75 270 0 22
button 10 295 2443 2444 1 0 10
text 15 295 0 23
text 75 295 0 24
button 10 320 2443 2444 1 0 11
text 15 320 0 25
text 75 320 0 26
button 10 345 2443 2444 1 0 12
text 15 345 0 27
text 75 345 0 28
button 10 370 2443 2444 1 0 13
text 15 370 0 29
text 75 370 0 30
text 430 290 2100 31
button 155 245 2443 2444 1 0 14
text 160 245 0 32
text 220 245 0 33
button 155 270 2443 2444 1 0 15
text 160 270 0 34
text 220 270 0 35
button 155 295 2443 2444 1 0 16
text 160 295 58 36
text 220 295 58 37
button 155 315 2443 2444 1 0 25
dtext 160 315 80 mWeight
dtext 220 315 80 <MODMAXWEIGHT>
button 155 355 4011 4013 1 0 17
dtext 187 355 1110 Empty the backpack
button 155 375 4011 4013 1 0 24
dtext 187 375 1120 Delete the backpack

[DIALOG d_CHARPROP2 TEXT]
Character Properties 2
1
2
3
Change Value
oSTR
<OSTR>
oINT
<OINT>
oDEX
<ODEX>
Hits
<HITS>
Mana
<MANA>
Stam
<STAMINA>
Karma
<KARMA>
Fame
<FAME>
Kills
<KILLS>
Food
<FOOD>
Flags
<FLAGS>
Plot1
<TAG0.PLOT1>
Plot2
<TAG0.PLOT2>
<DISPIDDEC>
Exp
<EXP>
Level
<LEVEL>
modAR
<MODAR>
modSTR
<MODSTR>
modINT
<MODINT>
modDEX
<MODDEX>
maxHits
<MAXHITS>
maxMana
<MAXMANA>
maxStam
<MAXSTAM>

[DIALOG d_CHARPROP2 BUTTON]

ON=801
	DIALOG=d_CHARPROP1

ON=803
	DIALOG=d_CHARPROP3

ON=1
	TRYP 4 INPDLG=OSTR 5

ON=2
	TRYP 4 INPDLG=OINT 5

ON=3
	TRYP 4 INPDLG=ODEX 5

ON=4
	TRYP 4 INPDLG=HITS 5

ON=5
	TRYP 4 INPDLG=MANA 5

ON=6
	TRYP 4 INPDLG=STAMINA 5

ON=7
	TRYP 4 INPDLG=KARMA 5

ON=8
	TRYP 4 INPDLG=FAME 5

ON=9
	TRYP 4 INPDLG=KILLS 5

ON=10
	TRYP 3 INPDLG=FOOD 4

ON=11
	TRYP 4 INPDLG=FLAGS 9

ON=12
	TRYP 6 INPDLG=TAG.PLOT1 4

ON=13
	TRYP 6 INPDLG=TAG.PLOT2 4

ON=14
	TRYP 4 INPDLG=EXP 6

ON=15
	TRYP 4 INPDLG=LEVEL 6

ON=16
	TRYP 4 INPDLG=MODAR 5

ON=17
	SRC.TARG.FINDLAYER(21).EMPTY

ON=18
	TRYP 6 INPDLG=MODSTR 5

ON=19
	TRYP 6 INPDLG=MODINT 5

ON=20
	TRYP 6 INPDLG=MODDEX 5

ON=21
	TRYP 4 INPDLG=MAXHITS 5

ON=22
	TRYP 4 INPDLG=MAXMANA 5

ON=23
	TRYP 4 INPDLG=MAXSTAM 5

ON=24
	SRC.TARG.FINDLAYER(21).REMOVE

ON=25
	TRYP 4 INPDLG=MODMAXWEIGHT 5

[DIALOG d_CHARPROP3]
70, 70
page 0
resizepic 0 0 5100 600 440
text 30 30 0 0
button 10 405 247 248 1 0 900
//button 10 400 5200 5201 1 0 900
//button 90 400 5204 5205 1 0 901
//button 170 400 5202 5203 1 0 902
//gumppic 325 15 2000
//tilepic 400 140 <DISPIDDEC>
text 10 50 80 4
text 210 50 80 4
text 410 50 80 4
button 410 25 2443 2444 1 0 51
text 285 25 1100 105
text 415 25 0 106
page 1
button 4 2 5006 5007 1 1 801
button 92 2 5006 5007 1 2 802
gumppic 180 2 5007
text 45 1 0 1
text 133 1 0 2
text 221 1 0 3
button 10 70 2443 2444 1 0 1
text 15 70 0 5
text 75 70 0 6
button 10 90 2443 2444 1 0 2
text 15 90 0 7
text 75 90 0 8
button 10 110 2443 2444 1 0 3
text 15 110 0 9
text 75 110 0 10
button 10 130 2443 2444 1 0 4
text 15 130 0 11
text 75 130 0 12
button 10 150 2443 2444 1 0 5
text 15 150 0 13
text 75 150 0 14
button 10 170 2443 2444 1 0 6
text 15 170 0 15
text 75 170 0 16
button 10 190 2443 2444 1 0 7
text 15 190 0 17
text 75 190 0 18
button 10 210 2443 2444 1 0 8
text 15 210 0 19
text 75 210 0 20
button 10 230 2443 2444 1 0 9
text 15 230 0 21
text 75 230 0 22
button 10 250 2443 2444 1 0 10
text 15 250 0 23
text 75 250 0 24
button 10 270 2443 2444 1 0 11
text 15 270 0 25
text 75 270 0 26
button 10 290 2443 2444 1 0 12
text 15 290 0 27
text 75 290 0 28
button 10 310 2443 2444 1 0 13
text 15 310 0 29
text 75 310 0 30
button 10 330 2443 2444 1 0 14
text 15 330 0 31
text 75 330 0 32
button 10 350 2443 2444 1 0 15
text 15 350 0 33
text 75 350 0 34
button 10 370 2443 2444 1 0 16
text 15 370 0 35
text 75 370 0 36
button 210 70 2443 2444 1 0 17
text 215 70 0 37
text 275 70 0 38
button 210 90 2443 2444 1 0 18
text 215 90 0 39
text 275 90 0 40
button 210 110 2443 2444 1 0 19
text 215 110 0 41
text 275 110 0 42
button 210 130 2443 2444 1 0 20
text 215 130 0 43
text 275 130 0 44
button 210 150 2443 2444 1 0 21
text 215 150 0 45
text 275 150 0 46
button 210 170 2443 2444 1 0 22
text 215 170 0 47
text 275 170 0 48
button 210 190 2443 2444 1 0 23
text 215 190 0 49
text 275 190 0 50
button 210 210 2443 2444 1 0 24
text 215 210 0 51
text 275 210 0 52
button 210 230 2443 2444 1 0 25
text 215 230 0 53
text 275 230 0 54
button 210 250 2443 2444 1 0 26
text 215 250 0 55
text 275 250 0 56
button 210 270 2443 2444 1 0 27
text 215 270 0 57
text 275 270 0 58
button 210 290 2443 2444 1 0 28
text 215 290 0 59
text 275 290 0 60
button 210 310 2443 2444 1 0 29
text 215 310 0 61
text 275 310 0 62
button 210 330 2443 2444 1 0 30
text 215 330 0 63
text 275 330 0 64
button 210 350 2443 2444 1 0 31
text 215 350 0 65
text 275 350 0 66
button 210 370 2443 2444 1 0 32
text 215 370 0 67
text 275 370 0 68
button 210 390 2443 2444 1 0 33
text 215 390 0 69
text 275 390 0 70
button 410 70 2443 2444 1 0 34
text 415 70 0 71
text 475 70 0 72
button 410 90 2443 2444 1 0 35
text 415 90 0 73
text 475 90 0 74
button 410 110 2443 2444 1 0 36
text 415 110 0 75
text 475 110 0 76
button 410 130 2443 2444 1 0 37
text 415 130 0 77
text 475 130 0 78
button 410 150 2443 2444 1 0 38
text 415 150 0 79
text 475 150 0 80
button 410 170 2443 2444 1 0 39
text 415 170 0 81
text 475 170 0 82
button 410 190 2443 2444 1 0 40
text 415 190 0 83
text 475 190 0 84
button 410 210 2443 2444 1 0 41
text 415 210 0 85
text 475 210 0 86
button 410 230 2443 2444 1 0 42
text 415 230 0 87
text 475 230 0 88
button 410 250 2443 2444 1 0 43
text 415 250 0 89
text 475 250 0 90
button 410 270 2443 2444 1 0 44
text 415 270 0 91
text 475 270 0 92
button 410 290 2443 2444 1 0 45
text 415 290 0 93
text 475 290 0 94
button 410 310 2443 2444 1 0 46
text 415 310 0 95
text 475 310 0 96
button 410 330 2443 2444 1 0 47
text 415 330 0 97
text 475 330 0 98
button 410 350 2443 2444 1 0 48
text 415 350 0 99
text 475 350 0 100
button 410 370 2443 2444 1 0 49
text 415 370 0 101
text 475 370 0 102
button 410 390 2443 2444 1 0 50
text 415 390 0 103
text 475 390 0 104

[DIALOG d_CHARPROP3 TEXT]
Skills
1
2
3
Change Value
<ALCHEMY>
Alchemy
<ANATOMY>
Anatomy
<ANIMALLORE>
AnimalLore
<ARCHERY>
Archery
<ARMSLORE>
ArmsLore
<BEGGING>
Begging
<BLACKSMITHING>
Blacksmithing
<BOWCRAFT>
Bowcraft
<CAMPING>
Camping
<CARPENTRY>
Carpentry
<CARTOGRAPHY>
Cartography
<COOKING>
Cooking
<DETECTINGHIDDEN>
DetectingHidden
<ENTICEMENT>
Enticement
<EVALUATINGINTEL>
EvaluatingIntel
<FENCING>
Fencing
<FISHING>
Fishing
<FORENSICS>
Forensics
	<HEALING>
	Healing
	<HERDING>
	Herding
	<HIDING>
	Hiding
	<INSCRIPTION>
	Inscription
	<ITEMID>
	ItemID
	<LOCKPICKING>
	LockPicking
	<LUMBERJACKING>
	Lumberjacking
	<MACEFIGHTING>
	Macefighting
	<MAGERY>
	Magery
	<MAGICRESISTANCE>
	MagicResistance
	<MEDITATION>
	Meditation
	<MINING>
	Mining
	<MUSICIANSHIP>
	Musicianship
	<NECROMANCY>
	Necromancy
	<PARRYING>
	Parrying
	<PEACEMAKING>
	Peacemaking
	<POISONING>
	Poisoning
	<PROVOCATION>
	Provocation
	<REMOVETRAP>
	RemoveTrap
	<SNOOPING>
	Snooping
	<SPIRITSPEAK>
	SpiritSpeak
	<STEALING>
	Stealing
	<STEALTH>
	Stealth
	<SWORDSMANSHIP>
	Swordsmanship
	<TACTICS>
	Tactics
	<TAILORING>
	Tailoring
	<TAMING>
	Taming
	<TASTEID>
	TasteID
	<TINKERING>
	Tinkering
	<TRACKING>
	Tracking
	<VETERINARY>
	Veterinary
	<WRESTLING>
	Wrestling
	Set Allskills to
	<SKILLTOTAL>

[DIALOG d_CHARPROP3 BUTTON]

ON=801
	DIALOG=d_CHARPROP1

ON=802
	DIALOG=d_CHARPROP2

ON=1
	TRYP 4 INPDLG=ALCHEMY 5

ON=2
	TRYP 4 INPDLG=ANATOMY 5

ON=3
	TRYP 4 INPDLG=ANIMALLORE 5

ON=4
	TRYP 4 INPDLG=ARCHERY 5

ON=5
	TRYP 4 INPDLG=ARMSLORE 5

ON=6
	TRYP 4 INPDLG=BEGGING 5

ON=7
	TRYP 4 INPDLG=BLACKSMITHING 5

ON=8
	TRYP 4 INPDLG=BOWCRAFT 5

ON=9
	TRYP 4 INPDLG=CAMPING 5

ON=10
	TRYP 4 INPDLG=CARPENTRY 5

ON=11
	TRYP 4 INPDLG=CARTOGRAPHY 5

ON=12
	TRYP 4 INPDLG=COOKING 5

ON=13
	TRYP 4 INPDLG=DETECTINGHIDDEN 5

ON=14
	TRYP 4 INPDLG=ENTICEMENT 5

ON=15
	TRYP 4 INPDLG=EVALUATINGINTEL 5

ON=16
	TRYP 4 INPDLG=FENCING 5

ON=17
	TRYP 4 INPDLG=FISHING 5

ON=18
	TRYP 4 INPDLG=FORENSICS 5

ON=19
	TRYP 4 INPDLG=HEALING 5

ON=20
	TRYP 4 INPDLG=HERDING 5

ON=21
	TRYP 4 INPDLG=HIDING 5

ON=22
	TRYP 4 INPDLG=INSCRIPTION 5

ON=23
	TRYP 4 INPDLG=ITEMID 5

ON=24
	TRYP 4 INPDLG=LOCKPICKING 5

ON=25
	TRYP 4 INPDLG=LUMBERJACKING 5

ON=26
	TRYP 4 INPDLG=MACEFIGHTING 5

ON=27
	TRYP 4 INPDLG=MAGERY 5

ON=28
	TRYP 4 INPDLG=MAGICRESISTANCE 5

ON=29
	TRYP 4 INPDLG=MEDITATION 5

ON=30
	TRYP 4 INPDLG=MINING 5

ON=31
	TRYP 4 INPDLG=MUSICIANSHIP 5

ON=32
	TRYP 4 INPDLG=NECROMANCY 5

ON=33
	TRYP 4 INPDLG=PARRYING 5

ON=34
	TRYP 4 INPDLG=PEACEMAKING 5

ON=35
	TRYP 4 INPDLG=POISONING 5

ON=36
	TRYP 4 INPDLG=PROVOCATION 5

ON=37
	TRYP 4 INPDLG=REMOVETRAP 5

ON=38
	TRYP 4 INPDLG=SNOOPING 5

ON=39
	TRYP 4 INPDLG=SPIRITSPEAK 5

ON=40
	TRYP 4 INPDLG=STEALING 5

ON=41
	TRYP 4 INPDLG=STEALTH 5

ON=42
	TRYP 4 INPDLG=SWORDSMANSHIP 5

ON=43
	TRYP 4 INPDLG=TACTICS 5

ON=44
	TRYP 4 INPDLG=TAILORING 5

ON=45
	TRYP 4 INPDLG=TAMING 5

ON=46
	TRYP 4 INPDLG=TASTEID 5

ON=47
	TRYP 4 INPDLG=TINKERING 5

ON=48
	TRYP 4 INPDLG=TRACKING 5

ON=49
	TRYP 4 INPDLG=VETERINARY 5

ON=50
	TRYP 4 INPDLG=WRESTLING 5

ON=51
	TRYP 4 INPDLG=ALLSKILLS 5

[DIALOG d_ITEMPROP1]
70, 70
page 0
resizepic 0 0 5100 550 360
text 30 30 0 0
//button 20 320 5200 5201 1 0 900		// cancel
//button 100 320 <eval g_btn_gray_apply> <eval g_btn_gray_apply_press> 1 0 901	// apply
//button 180 320 5202 5203 1 0 902	// save
button 20 320 5042 5043 1 0 902		// continue
button 180 320 5052 5053 1 0 903	// delete
button 210 320 5050 5051 1 0 904	// bounce
gumppic 275 15 2001
tilepic 350 140 <DISPIDDEC>
text 10 50 0 3
page 1
button 4 2 5007 5006 0 1
button 92 2 5006 5007 1 2 802
text 45 1 0 1
text 133 1 0 2
button 10 70 2443 2444 1 0 2
text 15 70 0 4
text 75 70 0 5
button 10 95 2443 2444 1 0 3
text 15 95 0 6
text 75 95 0 7
button 10 120 2443 2444 1 0 4
text 15 120 0 8
text 75 120 0 9
button 10 145 2443 2444 1 0 5
text 15 145 0 10
text 75 145 0 11
button 10 170 2443 2444 1 0 6
text 15 170 0 12
text 75 170 0 13
button 10 195 2443 2444 1 0 7
text 15 195 0 14
text 75 195 0 15
button 10 220 2443 2444 1 0 8
text 15 220 0 16
text 75 220 0 17
button 10 245 2443 2444 1 0 9
text 15 245 0 18
text 75 245 0 19
button 10 270 2443 2444 1 0 10
text 15 270 0 20
text 75 270 0 21
button 10 295 2443 2444 1 0 11
text 15 295 0 22
text 75 295 0 23

[DIALOG d_ITEMPROP1 TEXT]
Item Properties 1
1
2
Change  Value
Serial
<SERIAL>
BaseId
<BASEID>
Name
<NAME>
Color
<COLOR>
Timer
<TIMER>
Pos
<P>
Amount
<AMOUNT>
Type
<TYPE>
More1
<MORE1>
More2
<MORE2>

[DIALOG d_ITEMPROP1 BUTTON]

ON=903
	TRY REMOVE

ON=904
	TRY BOUNCE

ON=802
	DIALOG=d_ITEMPROP2

ON=2
	TRYP 255 INPDLG=SERIAL 8

ON=3
	TRYP 4 INPDLG=ID 32

ON=4
	TRYP 4 INPDLG=NAME 30

ON=5
	TRYP 4 INPDLG=COLOR 32

ON=6
	TRYP 4 INPDLG=TIMER 8

ON=7
	TRYP 4 INPDLG=P 16

ON=8
	TRYP 4 INPDLG=AMOUNT 5

ON=9
	TRYP 4 INPDLG=TYPE 32

ON=10
	TRYP 4 INPDLG=MORE1 32

ON=11
	TRYP 4 INPDLG=MORE2 32

[DIALOG d_ITEMPROP2]
70, 70
page 0
resizepic 0 0 5100 550 360
text 30 30 0 0
//button 20 320 5200 5201 1 0 900		// cancel
//button 100 320 <eval g_btn_gray_apply> <eval g_btn_gray_apply_press> 1 0 901	// apply
//button 180 320 5202 5203 1 0 902	// save
button 20 320 5042 5043 1 0 902		// continue
button 180 320 5052 5053 1 0 903	// delete
button 210 320 5050 5051 1 0 904	// bounce
gumppic 275 15 2001
tilepic 350 140 <DISPIDDEC>
text 10 50 0 3
page 1
button 4 2 5006 5007 1 1 801
button 92 2 5007 5006 0 2
text 45 1 0 1
text 133 1 0 2
button 10 70 2443 2444 1 0 12
text 15 70 0 4
text 75 70 0 5
button 10 95 2443 2444 1 0 13
text 15 95 0 6
text 75 95 0 7
button 10 120 2443 2444 1 0 14
text 15 120 0 8
text 75 120 0 9
button 10 145 2443 2444 1 0 15
text 15 145 0 10
text 75 145 0 11
button 10 170 2443 2444 1 0 16
text 15 170 0 12
text 75 170 0 13
button 10 195 2443 2444 1 0 17
text 15 195 0 14
text 75 195 0 15
button 10 220 2443 2444 1 0 18
text 15 220 0 16
text 75 220 0 17
button 10 245 2443 2444 1 0 19
text 15 245 0 18
text 75 245 0 19
button 10 270 2443 2444 1 0 20
text 15 270 0 20
text 75 270 0 21
button 10 295 2443 2444 1 0 21
text 15 295 0 22
text 75 295 0 23

[DIALOG d_ITEMPROP2 TEXT]
Item Properties 2
1
2
Change  Value
Attr
<ATTR>
Weight
<WEIGHT>
Layer
<LAYER>
Instances
<INSTANCES>
Armor
<ARMOR>
Value
<VALUE>
Dye
<DYE>
MoreP
<MOREP>
Link
<LINK>
DispID
<DISPID>

[DIALOG d_ITEMPROP2 BUTTON]

ON=903
	TRY REMOVE

ON=904
	TRY BOUNCE

ON=801
	DIALOG=d_ITEMPROP1

ON=12
	TRYP 4 INPDLG=ATTR 8

ON=13
	TRYP 4 INPDLG=WEIGHT 4

ON=14
	TRYP 5 INPDLG=LAYER 2

ON=15
	TRYP 4 INPDLG=INSTANCES 4

ON=16
	TRYP 4 INPDLG=ARMOR 4

ON=17
	TRYP 4 INPDLG=VALUE 7

ON=18
	TRYP 4 INPDLG=DYE 4

ON=19
	TRYP 4 INPDLG=MOREP 16

ON=20
	TRYP 4 INPDLG=LINK 8


[FUNCTION HelpPage]
// This function is called when client press "Help" button on paperdoll
SDIALOG d_helppage


[DIALOG d_helppage]
50,25

resizepic 0 0 2600 540 345
dhtmlgump 90 25 360 20 0 0 <DEF.CENTER><DEF.U>Ultima Online Help Menu<DEF.UE><DEF.CENTERE>
button 400 308 2073 2072 1 0 0

button 30 50 5540 5541 1 0 1
dhtmlgump 60 50 450 60 1 1 <DEF.U>General question about Ultima Online<DEF.UE>: Select this option if you are having difficulties learning to use a skill, if you have a general gameplay question, or you would like to search the UO Knowledge Base.

button 30 115 5540 5541 1 0 2
dhtmlgump 60 115 450 60 1 1 <DEF.U>My character is physically stuck<DEF.UE>: This choice only covers cases where your character is physically stuck in a location they cannot move out of.

button 30 180 5540 5541 1 0 3
dhtmlgump 60 180 450 60 1 1 <DEF.U>Other<DEF.UE>: If you are experiencing a problem in the game that does not fall into one of the other categories or is not addressed on the Support web page (located at <DEF.A>href="<SERV.URL>"><SERV.URL><DEF.AE>) and requires in-game assistance please use this option.

button 30 245 5540 5541 1 0 4
dhtmlgump 60 245 450 60 1 1 <DEF.U>Cancel pending request<DEF.UE>: Remove my pending help request from the queue.

[DIALOG d_helppage BUTTON]
ON=0
SYSMESSAGE @035 Help request aborted.

ON=1
WEBLINK www.uo.com/wiki/ultima-online-wiki/

ON=2
REF1=<FINDID.i_memory_helppage_stuck>
IF (<REF1>)
 SYSMESSAGE You choose not to go to any city.
 REF1.REMOVE
ELSE
 SYSMESSAGE Please choose your destination.
 CAN |= mt_nonmover
 SDIALOG d_helppage_stuck
ENDIF

ON=3
SDIALOG d_gmpage_prompt

ON=4
SYSMESSAGE @035 Removed help request.
IF (<SERV.GMPAGES>)
 FOR 0 <eval <SERV.GMPAGES>-1>
  IF !STRCMP("<SERV.GMPAGE.<LOCAL._FOR>.ACCOUNT>","<ACCOUNT>")
   GMPageDelete <LOCAL._FOR>
   return 1
  ENDIF
 ENDFOR
ENDIF


[DIALOG d_helppage_stuck]
150,50
nomove
noclose
nodispose

resizepic 0 0 2600 270 320
dhtmlgump 50 20 250 35 0 0 Choose a town:
FOR 1 6
 ARGS=<DEF.HelpPage.Teleport.<dLOCAL._FOR>>
 button 50 <eval 20+(<LOCAL._FOR>*35)> 208 209 1 0 <LOCAL._FOR>
 dhtmlgump 75 <eval 20+(<LOCAL._FOR>*35)> 145 20 0 0 <SERV.MAP(<dARGV[0]>,<dARGV[1]>,<dARGV[3]>).REGION.NAME>
ENDFOR
button 55 263 4005 4007 1 0 0
dhtmlgump 90 265 200 20 0 0 CANCEL

[DIALOG d_helppage_stuck BUTTON]
ON=0
SYSMESSAGE You choose not to go to any city.
CAN &= ~mt_nonmover

ON=1 6
IF (<HOUSEDESIGN>) || (<STONE>) || (<ACCOUNT.JAIL>)
 CAN &= ~mt_nonmover
 return 1
ENDIF
SYSMESSAGE You will be teleported within the next two minutes.
SERV.NEWITEM i_memory_helppage_stuck
NEW.MOREP=<DEF.HelpPage.Teleport.<ARGN>>
NEW.TIMER=<eval 10+<R110>>
NEW.EQUIP

[DEFNAME HelpPageTeleports]
HelpPage.Teleport.1	1519,1619,10	//Britain
HelpPage.Teleport.2	1993,2827	//Trinsic
HelpPage.Teleport.3	2910,727	//Vesper
HelpPage.Teleport.4	2433,541	//Minoc
HelpPage.Teleport.5	536,979		//Yew
HelpPage.Teleport.6	2247,1194	//Cove

[ITEMDEF i_memory_helppage_stuck]
ID=i_memory
NAME=HelpPage stuck teleport
TYPE=t_eq_script
ON=@Equip
 SRC.CAN |= mt_nonmover
ON=@Unequip
 SRC.CAN &= ~mt_nonmover
ON=@Timer
 CONT.GO <MOREP>
 REMOVE
 return 1


[DIALOG d_gmpage_prompt]
50,50

resizepic 0 0 2600 540 350
dhtmlgump 214 30 150 20 0 0 Enter Description
dhtmlgump 70 58 400 40 0 0 Please enter a brief description (up to 240 characters) of your problem:
resizepic 50 98 3500 440 200
dtextentrylimited 70 118 400 200 0 0 240
button 125 305 2076 2075 1 0 1
button 355 305 2073 2072 1 0 0

[DIALOG d_gmpage_prompt BUTTON]
ON=0
SYSMESSAGE @035 Help request aborted.

ON=1
GMPAGE ADD <ARGTXT[0]>


[FUNCTION page]
SDIALOG d_gmpage_queue


[DIALOG d_gmpage_queue]
50,25

page 0
resizepic 0 0 2600 540 410
dhtmlgump 90 25 360 20 0 0 <DEF.CENTER><DEF.U>GM Page Menu<DEF.UE><DEF.CENTERE>

page 1
LOCAL.Max=<SERV.GMPAGES>
IF (<LOCAL.Max> > 30)	// dialog can't be too huge or client will crash
 LOCAL.Max=30
ENDIF
IF (<LOCAL.Max>)
 LOCAL.Page=1
 FOR 0 <eval <LOCAL.Max>-1>
  IF (<LOCAL._FOR> > 0) && !(<eval <LOCAL._FOR>%5>)
   LOCAL.Page ++
   button 300 373 5540 5541 0 <LOCAL.Page> 0
   page <LOCAL.Page>
   button 220 373 5537 5538 0 <eval <LOCAL.Page>-1> 0
  ENDIF
  REF1=<SERV.GMPAGE.<LOCAL._FOR>.CHARUID>
  button 30 <eval 50+((<LOCAL._FOR>%5)*65)> 5540 5541 1 0 <eval <LOCAL._FOR>+1>
  dhtmlgump 60 <eval 50+((<LOCAL._FOR>%5)*65)> 450 60 1 1 <DEF.U><REF1.NAME><DEF.UE> <QVAL (<REF1.ISONLINE>)? <DEF.BFONT_DGREEN>(online):<DEF.BFONT_DRED>(offline)><DEF.BFONT_BLACK>: <SERV.GMPAGE.<LOCAL._FOR>.REASON>
 ENDFOR
ELSE
 dhtmlgump 60 50 450 20 0 0 GM page queue is empty
ENDIF

[DIALOG d_gmpage_queue BUTTON]
ON=1 50
REF1=<SERV.GMPAGE.<eval <ARGN>-1>.CHARUID>
IF (<REF1>)
 SRC.CTAG.Dialog.GMPage.Selected=<eval <ARGN>-1>
 REF1.DIALOG d_gmpage_info
ENDIF


[DIALOG d_gmpage_info]
50,25

resizepic 0 0 2600 540 410
dhtmlgump 90 25 360 20 0 0 <DEF.CENTER><DEF.U>GM Page Menu<DEF.UE><DEF.CENTERE>

dtext 65 50 0 Sent:
dtext 160 50 0 <FormatMinutes <eval <SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.TIME>/60>> ago

button 30 70 4005 4007 1 0 1
dtext 65 70 0 Player:
dtext 160 70 0 <NAME> (<QVAL (<ISONLINE>)? online:offline>)

ARGS=<SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.P>
button 30 90 4005 4007 1 0 2
dtext 65 90 0 Location:
dtext 160 90 0 <ARGS> (<SERV.MAP(<ARGV0>,<ARGV1>,<ARGV3>).REGION.REGION.NAME>)

REF1=<SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.HANDLED>
button 30 110 <QVAL ((<REF1>) && (<REF1> != <SRC>))? 4020 4022 0 0 0 : <QVAL (<REF1>)? 4002 4004 1 0 3 : 4008 4010 1 0 3>>
dtext 65 110 0 Handler:
dtext 160 110 0 <QVAL (<REF1>)? <REF1.NAME>:---->

button 30 130 <QVAL ((<REF1>) && (<REF1> != <SRC>))? 4020 4022 0 0 0 : 4017 4019 1 0 4>
dtext 65 130 0 Delete

dtext 65 150 0 Message:
dhtmlgump 160 150 350 100 1 1 <SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.REASON>

dtext 65 255 0 Response:
resizepic 160 255 3000 335 100
dtextentry 165 260 320 90 0 0 <SRC.CTAG.Dialog.GMPage.Response>
SRC.CTAG.Dialog.GMPage.Response=

button 247 373 <QVAL ((<REF1>) && (<REF1> != <SRC>))? 2074 2074 0 0 0 : 2076 2075 1 0 5>

[DIALOG d_gmpage_info BUTTON]
ON=0
SRC.SDIALOG d_gmpage_queue <eval 1+(<SRC.CTAG.Dialog.GMPage.Selected>/5)>
SRC.CTAG.Dialog.GMPage.Selected=

ON=1
SRC.GO <P>
SRC.CTAG.Dialog.GMPage.Response=""<ARGTXT[0]>""
SDIALOG d_gmpage_info

ON=2
SRC.GO <SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.P>
SRC.CTAG.Dialog.GMPage.Response=""<ARGTXT[0]>""
SDIALOG d_gmpage_info

ON=3
REF1=<SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.HANDLED>
IF (<REF1> == <SRC>)
 SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.HANDLED 0
ELIF (<SRC.GMPAGEP>)
 SRC.SYSMESSAGE @035 You are already handling another GM page.
ELIF !(<REF1>)
 SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.HANDLED <SRC>
ENDIF
SRC.CTAG.Dialog.GMPage.Response=""<ARGTXT[0]>""
SDIALOG d_gmpage_info

ON=4
REF1=<SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.HANDLED>
IF (<REF1>) && (<REF1> != <SRC>)
 SRC.CTAG.Dialog.GMPage.Response=""<ARGTXT[0]>""
 SDIALOG d_gmpage_info
 return 1
ENDIF
SRC.GMPageDelete <SRC.CTAG0.Dialog.GMPage.Selected>
SRC.SDIALOG d_gmpage_queue

ON=5
REF1=<SERV.GMPAGE.<SRC.CTAG0.Dialog.GMPage.Selected>.HANDLED>
IF (<REF1>) && (<REF1> != <SRC>)
 SRC.CTAG.Dialog.GMPage.Response=""<ARGTXT[0]>""
 SDIALOG d_gmpage_info
 return 1
ENDIF
ACCOUNT.TAG.GMPage.Response=""<ARGTXT[0]>""
IF (<ISONLINE>)
 DIALOGCLOSE d_gmpage_response
 SDIALOG d_gmpage_response
ENDIF
SRC.GMPageDelete <SRC.CTAG0.Dialog.GMPage.Selected>
SRC.SDIALOG d_gmpage_queue

[FUNCTION GMPageDelete]
SERV.GMPAGE.<ARGS>.DELETE
CTAG.Dialog.GMPage.Selected=
IF (<SERV.CLIENTS>)
 FOR 0 <eval <SERV.CLIENTS>-1>
  REF1=<SERV.CLIENT.<LOCAL._FOR>.UID>
  IF !(<isempty <REF1.CTAG.Dialog.GMPage.Selected>>)
   IF (<REF1.CTAG0.Dialog.GMPage.Selected> > <ARGS>)
    REF1.CTAG0.Dialog.GMPage.Selected --
   ELIF (<REF1.CTAG0.Dialog.GMPage.Selected> == <ARGS>)
    REF1.SYSMESSAGE @035 GM page cleared from queue
    REF1.CTAG.Dialog.GMPage.Selected=
    REF1.SDIALOG d_gmpage_queue
   ENDIF
  ELIF (<REF1.ISDIALOGOPEN.d_gmpage_queue>)
   REF1.DIALOGCLOSE d_gmpage_queue
   REF1.SDIALOG d_gmpage_queue
  ENDIF
 ENDFOR
ENDIF


[DIALOG d_gmpage_response]
0,0
noclose
nodispose

page 1
button 23 40 2117 2118 0 2 0
dtext 0 50 43 Message 

page 2
resizepic 50 25 2600 540 430
dhtmlgump 140 50 360 20 0 0 <DEF.CENTER><DEF.U>Ultima Online Help Response<DEF.UE><DEF.CENTERE>
dhtmlgump 80 90 480 290 1 1 GM tells <NAME>: <ACCOUNT.TAG.GMPage.Response>
dhtmlgump 80 390 480 20 0 0 Clicking the OKAY button will remove the response you have received.
button 400 417 2076 2075 1 0 1
button 475 417 2073 2072 0 1 0

[DIALOG d_gmpage_response BUTTON]
ON=1
ACCOUNT.TAG.GMPage.Response=


[DIALOG d_pet_release]
50,50

resizepic 0 0 5054 270 120
resizepic 10 10 3000 250 100
dhtmlgump 20 15 230 60 1 1 Are you sure you want to release your pet? Summoned Pets will vanish permanently!

button 20 80 4005 4007 1 0 1
dhtmlgump 55 80 75 20 0 0 CONTINUE

button 135 80 4005 4007 1 0 0
dhtmlgump 170 80 75 20 0 0 CANCEL

[DIALOG d_pet_release BUTTON]
ON=1
IF !(<SRC.CANSEELOS <UID>>) || !(<ISMYPET>)
 return 1
ENDIF
RELEASE


[DIALOG d_body_sculptor]
50,50

resizepic 50 10 2600 450 125
dhtmlgump 100 40 350 20 0 0 Choose your body modification:

button 100 75 4005 4007 1 0 1
dhtmlgump 140 75 300 20 0 0 New face style

[DIALOG d_body_sculptor BUTTON]
ON=1
IF !(<SRC.CANSEELOS <UID>>)
 return 1
ENDIF
SRC.CHANGEFACE


[DIALOG d_hairstylist]
50,50

ARGS=<QVAL ((<SRC.f_ishuman>) && !(<SRC.f_isfemale>))? "1,2,3,4,5,6,7,8" : "1,3,4">
LOCAL.Entries=<ARGS>

resizepic 50 10 2600 450 <eval 100+(<ARGV>*25)>
dhtmlgump 100 40 350 20 0 0 Choose your hairstyle change:
FOR <ARGV>
 ARGS=<DEF.HairStylist.Menu.<dSTRARG <LOCAL.Entries>>>
 button 100 <eval 75+((<LOCAL._FOR>-1)*25)> 4005 4007 1 0 <dSTRARG <LOCAL.Entries>>
 dhtmlgump 140 <eval 75+((<LOCAL._FOR>-1)*25)> 300 20 0 0 <ARGV[0]> (<ARGV[1]> gold)
 LOCAL.Entries=<STREAT <LOCAL.Entries>>
ENDFOR

[DIALOG d_hairstylist BUTTON]
ON=1 8
IF !(<SRC.CANSEELOS <UID>>)
 return 1
ELIF !((<SRC.f_ishuman>) && !(<SRC.f_isfemale>)) && !((<ARGN>==1) || (<ARGN>==3) || (<ARGN>==4))
 return 1
ENDIF
SRC.CTAG.Hairstylist.Dye.Palette=<QVAL ((<ARGN>==4) || (<ARGN>==7) || (<ARGN>==8))? "Bright" : "Normal">
ARGS=<DEF.HairStylist.Menu.<ARGN>>
SRC.CTAG.Hairstylist.Price=<ARGV[1]>
SRC.CTAG.Hairstylist.Dye.Hair=<ARGV[3]>
SRC.CTAG.Hairstylist.Dye.Beard=<ARGV[4]>
SDIALOG <ARGV[2]>

[DEFNAME HairStylistMenu]
HairStylist.Menu.1		New Hair,		50000,	d_hair_style
HairStylist.Menu.2		New Beard,		50000,	d_beard_style
HairStylist.Menu.3		Normal Hair Dye,	50,	d_hair_dye, 1, 1
HairStylist.Menu.4		Bright Hair Dye,	500000,	d_hair_dye, 1, 1
HairStylist.Menu.5		Hair Only Dye,		30000,	d_hair_dye, 1, 0
HairStylist.Menu.6		Beard Only Dye,		30000,	d_hair_dye, 0, 1
HairStylist.Menu.7		Bright Hair Only Dye,	500000,	d_hair_dye, 1, 0
HairStylist.Menu.8		Bright Beard Only Dye,	500000,	d_hair_dye, 0, 1


[DIALOG d_hair_style]
50,50

resizepic 0 0 2600 450 350
dhtmlgump 50 15 350 20 0 0 <DEF.CENTER>New Hairstyle<DEF.CENTERE>
button 45 290 4005 4007 1 0 1
dhtmlgump 77 290 300 20 0 0 OK
button 270 290 4005 4007 1 0 0
dhtmlgump 302 290 300 20 0 0 Cancel

LOCAL.Race=<QVAL (<SRC.f_iself>)? "Elf" : "Human">
LOCAL.Gender=<QVAL (<SRC.f_isfemale>)? "Female" : "Male">
FOR 9
 LOCAL.X=<eval ((<LOCAL._FOR>-1)%3)*123>
 LOCAL.Y=<eval ((<LOCAL._FOR>-1)/3)*65>
 ARGS=<DEF.Hair.Style.<LOCAL.Race>.<dLOCAL._FOR>.<LOCAL.Gender>>
 IF !(<isempty <ARGS>>)
  radio <eval 40+<LOCAL.X>> <eval 70+<LOCAL.Y>> 208 209 0 <LOCAL._FOR>
  resizepic <eval 87+<LOCAL.X>> <eval 50+<LOCAL.Y>> 2620 50 50
  gumppic <eval 87+<LOCAL.X>+<ARGV[2]>> <eval 50+<LOCAL.Y>+<ARGV[3]>> <ARGV[1]>
 ENDIF
ENDFOR
radio 40 240 208 209 0 10
dhtmlgump 85 240 180 35 0 0 Bald (You will lose your hair color.)

[DIALOG d_hair_style BUTTON]
ON=1 10
IF !(<SRC.CANSEELOS <UID>>)
 return 1
ELIF !(<SRC.ISGM>)
 IF (<SRC.GOLD> < <SRC.CTAG0.Hairstylist.Price>)
  SAY You cannot afford my services for that style.
  return 1
 ENDIF
 SRC.GOLD -= <SRC.CTAG0.Hairstylist.Price>
 SRC.SYSMESSAGE <dSRC.CTAG0.Hairstylist.Price> gold has been removed from your bank box.
ENDIF

LOCAL.Race=<QVAL (<SRC.f_iself>)? "Elf" : "Human">
LOCAL.Gender=<QVAL (<SRC.f_isfemale>)? "Female" : "Male">
ARGS=<DEF.Hair.Style.<LOCAL.Race>.<ARGCHKID>.<LOCAL.Gender>>
REF1=<SRC.FINDLAYER.layer_hair>
IF (<REF1>)
 LOCAL.Color=<REF1.COLOR>
 REF1.REMOVE
ENDIF
IF !(<isempty <ARGV[0]>>)
 SERV.NEWITEM <ARGV[0]>
 NEW.COLOR=<LOCAL.Color>
 NEW.EQUIP <SRC>
ENDIF

[DEFNAME HairStyles]
Hair.Style.Human.1.Male		i_hair_short,		0c60c,	-67,	-40
Hair.Style.Human.1.Female	i_hair_short,		0ed1c,	-67,	-40
Hair.Style.Human.2.Male		i_hair_pageboy,		0ed26,	-67,	-42
Hair.Style.Human.2.Female	i_hair_pageboy,		0ed26,	-67,	-42
Hair.Style.Human.3.Male		i_hair_mohawk,		0c60f,	-67,	-35
Hair.Style.Human.3.Female	i_hair_mohawk,		0ed27,	-67,	-35
Hair.Style.Human.4.Male		i_hair_long,		0c60d,	-67,	-50
Hair.Style.Human.4.Female	i_hair_long,		0ed1d,	-67,	-50
Hair.Style.Human.5.Male		i_hair_curly,		0c618,	-67,	-40
Hair.Style.Human.5.Female	i_hair_curly,		0ed25,	-67,	-40
Hair.Style.Human.6.Male		i_hair_topknot,		0ed29,	-63,	-40
Hair.Style.Human.6.Female	i_hair_topknot,		0ed29,	-63,	-40
Hair.Style.Human.7.Male		i_hair_ponytail,	0c60e,	-67,	-50
Hair.Style.Human.7.Female	i_hair_ponytail,	0ed1e,	-67,	-50
Hair.Style.Human.8.Male		i_hair_2_pigtails,	0c612,	-67,	-50
Hair.Style.Human.8.Female	i_hair_2_pigtails,	0ede6,	-67,	-50
Hair.Style.Human.9.Male		i_hair_receding,	0ede5,	-68,	-40
Hair.Style.Human.9.Female	i_hair_buns,		0ed28,	-68,	-40

Hair.Style.Elf.1.Male		i_hair_long_feather,	0c6e5,	-67,	-42
Hair.Style.Elf.1.Female		i_hair_long_feather,	0edf5,	-65,	-42
Hair.Style.Elf.2.Male		i_hair_short1,		0c6e6,	-67,	-40
Hair.Style.Elf.2.Female		i_hair_short1,		0edf6,	-67,	-42
Hair.Style.Elf.3.Male		i_hair_elven_mullet,	0c6e7,	-67,	-42
Hair.Style.Elf.3.Female		i_hair_elven_mullet,	0edf7,	-67,	-45
Hair.Style.Elf.4.Male		i_hair_elven_topknot,	0c6cc,	-67,	-40
Hair.Style.Elf.4.Female		i_hair_elven_topknot,	0eddc,	-67,	-42
Hair.Style.Elf.5.Male		i_hair_elven_braid,	0c6cd,	-67,	-42
Hair.Style.Elf.5.Female		i_hair_elven_braid,	0eddd,	-67,	-45
Hair.Style.Elf.6.Male		i_hair_elven_spike,	0c6cf,	-67,	-40
Hair.Style.Elf.6.Female		i_hair_elven_spike,	0eddf,	-65,	-40
Hair.Style.Elf.7.Male		i_hair_elven_ml,	0c6e4,	-67,	-42
Hair.Style.Elf.7.Female		i_hair_elven_flower,	0edda,	-65,	-45
Hair.Style.Elf.8.Male		i_hair_elven_long,	0c6cb,	-67,	-45
Hair.Style.Elf.8.Female		i_hair_elven_buns,	0edde,	-65,	-45


[DIALOG d_beard_style]
50,50

resizepic 0 0 2600 310 390
dhtmlgump 55 15 200 20 0 0 <DEF.CENTER>New Beard<DEF.CENTERE>
button 45 340 4005 4007 1 0 1
dhtmlgump 77 340 300 20 0 0 OK
button 170 340 4005 4007 1 0 0
dhtmlgump 202 340 300 20 0 0 Cancel

FOR 8
 LOCAL.X=<eval ((<LOCAL._FOR>-1)%2)*123>
 LOCAL.Y=<eval ((<LOCAL._FOR>-1)/2)*70>
 ARGS=<DEF.Beard.Style.Human.<dLOCAL._FOR>>
 radio <eval 40+<LOCAL.X>> <eval 90+<LOCAL.Y>> 208 209 0 <LOCAL._FOR>
 IF !(<isempty <ARGS>>)
  resizepic <eval 87+<LOCAL.X>> <eval 70+<LOCAL.Y>> 2620 50 50
  gumppic <eval 87+<LOCAL.X>+<ARGV[2]>> <eval 70+<LOCAL.Y>+<ARGV[3]>> <ARGV[1]>
 ENDIF
ENDFOR

[DIALOG d_beard_style BUTTON]
ON=1 8
IF !(<SRC.CANSEELOS <UID>>)
 return 1
ELIF !(<SRC.ISGM>)
 IF (<SRC.GOLD> < <SRC.CTAG0.Hairstylist.Price>)
  SAY You cannot afford my services for that style.
  return 1
 ENDIF
 SRC.GOLD -= <SRC.CTAG0.Hairstylist.Price>
 SRC.SYSMESSAGE <dSRC.CTAG0.Hairstylist.Price> gold has been removed from your bank box.
ENDIF

ARGS=<DEF.Beard.Style.Human.<ARGCHKID>>
REF1=<SRC.FINDLAYER.layer_beard>
IF (<REF1>)
 LOCAL.Color=<REF1.COLOR>
 REF1.REMOVE
ENDIF
IF !(<isempty <ARGV[0]>>)
 SERV.NEWITEM <ARGV[0]>
 NEW.COLOR=<LOCAL.Color>
 NEW.EQUIP <SRC>
ENDIF

[DEFNAME BeardStyles]
Beard.Style.Human.1		i_beard_goatee,		0c670,	-67,	-50
Beard.Style.Human.2		i_beard_short_med,	0c6d8,	-67,	-47
Beard.Style.Human.3		i_beard_vandyke,	0c6da,	-67,	-50
Beard.Style.Human.4		i_beard_long,		0c671,	-67,	-55
Beard.Style.Human.5		i_beard_short,		0c672,	-67,	-47
Beard.Style.Human.6		i_beard_long_med,	0c6d9,	-67,	-55
Beard.Style.Human.7		i_beard_mustache,	0c678,	-67,	-48


[DIALOG d_hair_dye]
50,50

page 0
resizepic 100 10 2600 350 355
resizepic 120 54 5100 110 270
dhtmlgump 70 25 400 35 0 0 <DEF.CENTER>Hair Color Selection Menu<DEF.CENTERE>
button 149 328 4005 4007 1 0 1
dhtmlgump 185 329 250 35 0 0 Dye my hair this color!

WHILE !(<isempty <DEF.Hair.Dye.<SRC.CTAG0.Hairstylist.Dye.Palette>.<eval <LOCAL._WHILE>+1>>>)
 ARGS=<DEF.Hair.Dye.<SRC.CTAG0.Hairstylist.Dye.Palette>.<eval <LOCAL._WHILE>+1>>
 dtext 130 <eval 59+(<LOCAL._WHILE>*22)> <eval <ARGV0>-1> *****
 button 207 <eval 60+(<LOCAL._WHILE>*22)> 5224 5224 0 <eval <LOCAL._WHILE>+1> 0
ENDWHILE

WHILE !(<isempty <DEF.Hair.Dye.<SRC.CTAG0.Hairstylist.Dye.Palette>.<eval <LOCAL._WHILE>+1>>>)
 ARGS=<DEF.Hair.Dye.<SRC.CTAG0.Hairstylist.Dye.Palette>.<eval <LOCAL._WHILE>+1>>
 page <eval <LOCAL._WHILE>+1>
 LOCAL.N=0
 FOR <ARGV0> <ARGV1>
  dtext <eval 278+((<LOCAL.N>/16)*80)> <eval 52+((<LOCAL.N>%16)*17)> <eval <LOCAL._FOR>-1> *****
  radio <eval 260+((<LOCAL.N>/16)*80)> <eval 52+((<LOCAL.N>%16)*17)> 210 211 0 <LOCAL._FOR>
  LOCAL.N ++
 ENDFOR
ENDWHILE

[DIALOG d_hair_dye BUTTON]
ON=1 2500
IF !(<SRC.CANSEELOS <UID>>)
 return 1
ELIF !(<SRC.ISGM>)
 IF (<SRC.GOLD> < <SRC.CTAG0.Hairstylist.Price>)
  SAY You cannot afford my services for that style.
  return 1
 ENDIF
 SRC.GOLD -= <SRC.CTAG0.Hairstylist.Price>
 SRC.SYSMESSAGE <dSRC.CTAG0.Hairstylist.Price> gold has been removed from your bank box.
ENDIF

WHILE !(<isempty <DEF.Hair.Dye.<SRC.CTAG0.Hairstylist.Dye.Palette>.<eval <LOCAL._WHILE>+1>>>)
 ARGS=<DEF.Hair.Dye.<SRC.CTAG0.Hairstylist.Dye.Palette>.<eval <LOCAL._WHILE>+1>>
 IF (<ARGCHKID> >= <ARGV0>) && (<ARGCHKID> <= <ARGV1>)
  IF (<SRC.CTAG0.Hairstylist.Dye.Hair>)
   SRC.FINDLAYER.layer_hair.COLOR=<ARGCHKID>
  ENDIF
  IF (<SRC.CTAG0.Hairstylist.Dye.Beard>)
   SRC.FINDLAYER.layer_beard.COLOR=<ARGCHKID>
  ENDIF
  return 1
 ENDIF
ENDWHILE

[DEFNAME HairDyes]
Hair.Dye.Normal.1		1602, 1627	//Brown
Hair.Dye.Normal.2		1628, 1654	//Chestnut
Hair.Dye.Normal.3		1502, 1533	//Auburn
Hair.Dye.Normal.4		1302, 1333	//Blue
Hair.Dye.Normal.5		1402, 1433	//Green
Hair.Dye.Normal.6		1202, 1225	//Red
Hair.Dye.Normal.7		2402, 2430	//Light
Hair.Dye.Normal.8		2213, 2218	//Blonde
Hair.Dye.Normal.9		1102, 1109	//Black
Hair.Dye.Normal.10		1110, 1117	//Beige
Hair.Dye.Normal.11		1118, 1133	//Golden
Hair.Dye.Normal.12		1134, 1149	//Dark Brown

Hair.Dye.Bright.1		12, 21		//Purple
Hair.Dye.Bright.2		32, 36		//Red
Hair.Dye.Bright.3		38, 45		//Orange
Hair.Dye.Bright.4		54, 56		//Yellow
Hair.Dye.Bright.5		62, 71		//Green
Hair.Dye.Bright.6		81, 82		//Light Green
Hair.Dye.Bright.7		89, 90		//Blue
Hair.Dye.Bright.8		1153, 1154	//White

[EOF]